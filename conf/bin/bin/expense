#!/usr/bin/awk -E
# From: <http://c2.com/doc/expense/>
# Modified: Jun  4 2016 by @meatcar: indent for markdown, format 0s
# Modified: Feb  2 2018 by @meatcar: allow multiplying or dividing two symbols
# Modified: Feb  2 2018 by @meatcar: use awk as an interpreter
# Modified: Feb  2 2018 by @meatcar: support dashes and underscores in symbols

BEGIN { symbol = "^[A-Z]+[-_A-Z0-9]*$" }

tolower($0) ~ /#expense end/ {exit}

$1 ~ symbol {
	if (sums[$1] == "" || $1 == "SUM") {
		sums[$1] = sum			# Define Symbol
		$1 = sum
		sum = 0
	}
	else {
		$1 = sums[$1]			# Dereference Symbol
	}
}

# Dereference Symbol for Explicit Calculations
($2 == "*" || $2 == "/") && $3 ~ symbol {
	if (sums[$3] == "") {
		print("Symbol undefined:", $3) > "/dev/stderr"
	} else {
		$3 = sums[$3]
	}
}
	                            # Pretty Print
$1 ~ /^-?[0-9]+\.?[0-9]*/ {$1 = sprintf("    %7.2f", $1)}
		{print}

$2 == "*"	{$1 *= $3}			# Explicit Calculations
$2 == "/"	{$1 /= $3}
$2 == "DB"	{$1 = -$1}
$2 == "CR"	{$1 = -$1}

NF == 0		{sum = 0}			# Implicit Summation

{
	sum += $1
	$1 = sprintf("%s +", $1)
}


